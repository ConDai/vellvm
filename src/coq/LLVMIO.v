(* -------------------------------------------------------------------------- *
 *                     Vellvm - the Verified LLVM project                     *
 *                                                                            *
 *     Copyright (c) 2017 Steve Zdancewic <stevez@cis.upenn.edu>              *
 *                                                                            *
 *   This file is distributed under the terms of the GNU General Public       *
 *   License as published by the Free Software Foundation, either version     *
 *   3 of the License, or (at your option) any later version.                 *
 ---------------------------------------------------------------------------- *)

From Coq Require Import
     ZArith
     List
     String
     Setoid
     Morphisms
     Omega
     Classes.RelationClasses.

From ExtLib Require Import
     Core.RelDec
     Programming.Eqv
     Programming.Show
     Structures.Monads.

From ITree Require Import
     ITree
     Effects.Exception.

From Vellvm Require Import
     Util
     LLVMAst
     MemoryAddress
     DynamicValues
     LocalEnvironment
     Error.

From Paco Require Import
     paco.

Set Implicit Arguments.
Set Contextual Implicit.

Inductive dtyp : Set :=
| DTYPE_I (sz:Z)
| DTYPE_Pointer
| DTYPE_Void
| DTYPE_Half
| DTYPE_Float
| DTYPE_Double
| DTYPE_X86_fp80
| DTYPE_Fp128
| DTYPE_Ppc_fp128
| DTYPE_Metadata
| DTYPE_X86_mmx
| DTYPE_Array (sz:Z) (t:dtyp)
| DTYPE_Struct (fields:list dtyp)
| DTYPE_Packed_struct (fields:list dtyp)
| DTYPE_Opaque
| DTYPE_Vector (sz:Z) (t:dtyp)     (* t must be integer, floating point, or pointer type *)
.

Section hiding_notation.
  Import ShowNotation.
  Local Open Scope show_scope.

  Fixpoint show_dtyp' (dt:dtyp) :=
    match dt with
    | DTYPE_I sz     => ("i" << show sz)
    | DTYPE_Pointer  => "ptr"
    | DTYPE_Void     => "dvoid"
    | DTYPE_Half     => "half"
    | DTYPE_Float    => "float"
    | DTYPE_Double   => "double"
    | DTYPE_X86_fp80 => "x86_fp80"
    | DTYPE_Fp128    => "fp128"
    | DTYPE_Ppc_fp128 => "ppc_fp128"
    | DTYPE_Metadata  => "metadata"
    | DTYPE_X86_mmx   => "x86_mmx"
    | DTYPE_Array sz t
          => ("[" << show sz << " x " << (show_dtyp' t) << "]")
    | DTYPE_Struct fields
          => ("{" << iter_show (List.map (fun x => (show_dtyp' x) << ",") fields) << "}")
    | DTYPE_Packed_struct fields
      => ("packed{" << iter_show (List.map (fun x => (show_dtyp' x) << ",") fields) << "}")
    | DTYPE_Opaque => "opaque"
    | DTYPE_Vector sz t
      => ("<" << show sz << " x " << (show_dtyp' t) << ">")  (* TODO: right notation? *)
    end%string.

  Global Instance show_dtyp : Show dtyp := show_dtyp'.
End hiding_notation.


Module Type LLVM_INTERACTIONS (ADDR : MemoryAddress.ADDRESS).

Global Instance eq_dec_addr : RelDec (@eq ADDR.addr) := RelDec_from_dec _ ADDR.addr_dec.
Global Instance Eqv_addr : Eqv ADDR.addr := (@eq ADDR.addr).

(* The set of dynamic types manipulated by an LLVM program.  Mostly
   isomorphic to LLVMAst.typ but
     - pointers have no further detail
     - identified types are not allowed
   Questions:
     - What to do with Opaque?
*)

Module DV := DynamicValues.DVALUE(ADDR).
Export DV.

Module LCLS := LocalEnvironment.LLVM_LOCALS(ADDR).
Export LCLS.

Inductive Void :=.

(* Call event for the LLVM IR *)
(* YZ : Should f be a string or a function_id?
 * SAZ: this should be function_id and just one Call event

   %ans = call <t> @foo(args)

   call <void> @foo(args)
 *)
Inductive CallE: Type -> Type :=
| Call        : forall (t:dtyp) (f:function_id) (args:list dvalue), CallE dvalue.

Inductive ExternalCallE: Type -> Type :=
| ExternalCall : forall (t:dtyp) (f:function_id) (args:list dvalue), ExternalCallE dvalue.

(* IO Interactions for the LLVM IR *)
Inductive IO : Type -> Type :=
| Alloca : forall (t:dtyp), (IO dvalue)
| Load   : forall (t:dtyp) (a:dvalue), (IO dvalue)
| Store  : forall (a:dvalue) (v:dvalue), (IO unit)
| GEP    : forall (t:dtyp) (v:dvalue) (vs:list dvalue), (IO dvalue)
| ItoP   : forall (i:dvalue), (IO dvalue)
| PtoI   : forall (a:dvalue), (IO dvalue)
.

(* Trace of events generated by a computation. *)
Definition LLVM E := itree (CallE +' ExternalCallE +' Locals +' IO +' E).
Hint Unfold LLVM.

(* Trace Utilities ---------------------------------------------------------- *)

(* Debug is identical to the "Trace" effect from the itrees library,
   but debug is probably a less confusing name for us. *)
Variant debugE : Type -> Type :=
| Debug : string -> debugE unit.

Definition debug {E} `{debugE -< E} (msg : string) : itree E unit :=
  send (Debug msg).

Definition debug_hom {E} (R : Type) (e : debugE R) : itree E R :=
  match e with
  | Debug _ => Ret tt
  end.

Definition into_debug {E F} (h : E ~> LLVM F) : CallE +' ExternalCallE +' Locals +' IO +' (F +' E) ~> LLVM F :=
  fun x e =>
    match e with
    | inr1 (inr1 (inr1 (inr1 (inr1 e)))) => h _ e
    | inr1 (inr1 (inr1 (inr1 (inl1 e)))) => vis e (fun x => Ret x)
    | inr1 (inr1 (inr1 (inl1 e))) => vis e (fun x => Ret x)
    | inr1 (inr1 (inl1 e)) => vis e (fun x => Ret x)
    | inr1 (inl1 e) => vis e (fun x => Ret x)
    | inl1 e => vis e (fun x => Ret x)
    end.

Definition ignore_debug {E} : LLVM (E +' debugE) ~> LLVM E :=
  interp (into_debug debug_hom).

(* Failures carry string *)
Definition failureE := exceptE string.

Definition lift_err {A B} (f : A -> LLVM (failureE +' debugE) B) (m:err A) : LLVM (failureE +' debugE) B :=
  match m with
  | inl x => throw x
  | inr x => f x
  end.

Notation "'do' x <- m ;; f" := (lift_err (fun x => f) m)
                                (at level 100, x ident, m at next level, right associativity).


Definition raise_FailureE {E} `{failureE -< E} := @throw _ E _.
CoFixpoint catch_FailureE {E F X} `{E -< F} `{failureE -< F}
           (f:string -> itree F X)
           (t : itree (failureE +' E) X) : itree F X :=
  match (observe t) with
  | RetF x => Ret x
  | TauF t => Tau (catch_FailureE f t)
  | VisF _ (inl1 (Throw s)) k => f s
  | VisF _ (inr1 e) k => vis e (fun x => catch_FailureE f (k x))
  end.

(* CB TODO: Can we have 1 instance of MonadExc? *)
Global Instance monad_exc_FailureE {E} : (MonadExc string (itree (failureE +' E))) := {
  raise := raise_FailureE ;
  catch := fun T m f => catch_FailureE f m ;
}.

CoFixpoint catch_FailureE1E2 {E1 E2 F X} `{E1 -< F} `{E2 -< F} `{failureE -< F}
           (f:string -> itree F X)
           (t : itree (E1 +' failureE +' E2) X) : itree F X :=
  match (observe t) with
  | RetF x => Ret x
  | TauF t => Tau (catch_FailureE1E2 f t)
  | VisF _ (inr1 (inl1 (Throw s))) k => f s
  | VisF _ e k => vis e (fun x => catch_FailureE1E2 f (k x))
  end.


(* CB TODO: Can we have 1 instance of MonadExc? *)
Global Instance monad_exc_FailureE1E2 {E1 E2} : (MonadExc string (itree (E1 +' failureE +' E2))) := {
  raise := raise_FailureE ;
  catch := fun T m f => catch_FailureE1E2 f m ;
}.


End LLVM_INTERACTIONS.


Module Make(ADDR : MemoryAddress.ADDRESS) <: LLVM_INTERACTIONS(ADDR).
Include LLVM_INTERACTIONS(ADDR).
End Make.
